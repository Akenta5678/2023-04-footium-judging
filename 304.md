tsvetanovv

medium

# When the user mint players, he didn't receive overpay ETH

## Summary
In `FootiumAcademy.sol` we have [mintPlayers()](https://github.com/sherlock-audit/2023-04-footium/blob/main/footium-eth-shareable/contracts/FootiumAcademy.sol#L166-L212) function:
```solidity
function mintPlayers(
        SeasonID seasonId,
        uint256 clubId,
        uint256 divisionTier,
        uint256[] calldata generationIds,
        bytes32[] calldata divisionProof
    ) external payable whenNotPaused nonReentrant {  
        uint256 totalFee = _validateMintingParams(
            seasonId,
            clubId,
            divisionTier,
            generationIds,
            divisionProof
        );

        uint256 generationId;

        for (uint256 i = 0; i < generationIds.length; ) {
            generationId = generationIds[i];
  
            if (generationId > _maxGenerationId) {
                revert GenerationIDTooHigh(generationId, _maxGenerationId);
            }

            if (redeemed[seasonId][clubId][generationId]) {
                revert PlayerAlreadyRedeemed(generationId);
            }

            redeemed[seasonId][clubId][generationId] = true;

            uint256 playerId = _footiumPlayer.safeMint(
                _footiumClub.clubToEscrow(clubId)
            );

            emit AcademyPlayerMinted(seasonId, clubId, generationId, playerId);

            unchecked {
                i++;
            }
        }

        // forward total fee to the prize distributor contract
        (bool sent, ) = _prizeDistributorAddress.call{value: totalFee}("");
        if (!sent) {
            revert FailedToSendETH(totalFee);
        }
    }
```
This function mints new "players" corresponding to a specific season and for a specific club. The owner of the contract must pay a fee to mint the new players.

To calculate the total fee the function calls `_validateMintingParams()`:
```solidity
function _validateMintingParams(
        SeasonID seasonId,
        uint256 clubId,
        uint256 divisionTier,
        uint256[] calldata generationIds,
        bytes32[] calldata divisionProof
    ) private returns (uint256) {
        if (
            !MerkleProofUpgradeable.verify(divisionTier
                divisionProof,
                _clubDivsMerkleRoot,
                keccak256(abi.encodePacked(clubId, divisionTier))
            )
        ) {
            revert ClubNotInDivision(clubId, divisionTier);
        }

        if (msg.sender != _footiumClub.ownerOf(clubId)) {
            revert NotClubOwner(clubId, msg.sender);
        }

        if (SeasonID.unwrap(seasonId) <= 0) {
            revert InvalidSeasonId(seasonId);
        }

        if (SeasonID.unwrap(seasonId) > currentSeasonId) {
            revert PlayerTooYoung(seasonId);
        }

        uint256 playerCount = generationIds.length;
        uint256 totalFee = playerCount * divisionToFee[divisionTier];
        if (msg.value < totalFee) {
            revert IncorrectETHAmount(msg.value);
        }

        uint256 maxSeasonId = SeasonID.unwrap(seasonId) +
            academyMaxAge -
            academyMinAge;


        if (maxSeasonId < currentSeasonId) {
            revert PlayerTooOld(currentSeasonId);
        }
  
        return totalFee;

    }
```
## Vulnerability Detail

As we can see from the code snippets, the function validates the parameters and computes them.
```solidity
258: uint256 totalFee = playerCount * divisionToFee[divisionTier];
```
Then there is a check if the user has sent enough eth:
```solidity
259: if (msg.value < totalFee) {
260:     revert IncorrectETHAmount(msg.value);
261  }
```
But nowhere is there a calculation if the user sends more eth than necessary. Then the difference in the amount will not be returned but will be lost.

## Impact

Excess ETH for the mint player fee is not refunded and this leads to loss of user funds.

## Code Snippet
https://github.com/sherlock-audit/2023-04-footium/blob/main/footium-eth-shareable/contracts/FootiumAcademy.sol#L166-L212

## Tool used

Manual Review

## Recommendation

Consider refunding the remaining ETH to the caller at the end of the `_validateMintingParams()` function like:

```solidity
if (msg.value > totalFee) {
    msg.value.safeTransferETH(msg.value - totalFee);
}
```
