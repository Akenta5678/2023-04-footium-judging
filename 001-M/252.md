juancito

high

# Some ERC20 tokens can get permanently stuck in the contract due to use of `transfer()`

## Summary

Some popular tokens like USDT do not return any value when they perform a `transfer()`.

This breaks the compatibility with the `IERC20` interface, which will end up reverting the functions that use it.

## Vulnerability Detail

`FootiumEscrow::transferERC20()` makes use of `transfer()` to transfer tokens out of the contract. `FootiumEscrow` is not upgradeable, so tokens transferred to it, won't be able to be recovered:

```solidity
    function transferERC20(
        IERC20 erc20Contract,
        address to,
        uint256 amount
    ) external onlyClubOwner {
        erc20Contract.transfer(to, amount); // @audit
    }
```

`FootiumPrizeDistributor::claimERC20Prize()` uses it to distribute prizes. `FootiumPrizeDistributor` is upgradeable, so tokens would be able to be distributed after a fix and an upgrade.

```solidity
    function claimERC20Prize(
        address _to,
        IERC20Upgradeable _token,
        uint256 _amount,
        bytes32[] calldata _proof
    ) external whenNotPaused nonReentrant {
        if (_to != msg.sender) {
            revert InvalidAccount();
        }

        if (
            !MerkleProofUpgradeable.verify(
                _proof,
                erc20MerkleRoot,
                keccak256(abi.encodePacked(_token, _to, _amount))
            )
        ) {
            revert InvalidERC20MerkleProof();
        }

        uint256 value = _amount - totalERC20Claimed[_token][_to];

        if (value > 0) {
            totalERC20Claimed[_token][_to] += value;
            _token.transfer(_to, value); // @audit
        }

        emit ClaimERC20(_token, _to, value);
    }
```

## Impact

Permanently stuck ERC20 tokens in `FootiumEscrow` contracts.

Temporarily stuck ERC20 tokens in `FootiumPrizeDistributor` until an upgrade is made.

## Code Snippet

https://github.com/sherlock-audit/2023-04-footium/blob/main/footium-eth-shareable/contracts/FootiumEscrow.sol#L110

https://github.com/sherlock-audit/2023-04-footium/blob/main/footium-eth-shareable/contracts/FootiumPrizeDistributor.sol#L130

## Tool used

Manual Review

## Recommendation

Use `safeTransfer` instead of `transfer`. This way, tokens that do not return any value will succeed in transfering the assets.