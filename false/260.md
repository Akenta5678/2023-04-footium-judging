juancito

medium

# `claimETHPrize` and `claimERC20Prize` are claimable by the receiver instead of club owners

## Summary

The [Footium Prize Distributor Documentation](https://github.com/sherlock-audit/2023-04-footium/blob/main/footium-eth-shareable/contracts/technical-docs/FootiumPrizeDistributor.md#claimerc20prizeaddress-_to-ierc20upgradeable-_token-uint256-_amount-bytes32-calldata-_proof) for `claimERC20Prize()` and `claimETHPrize` states: 

> The function checks to see whether the function caller owns the club that they're claiming prizes for. If successful, the function transfers ETH / ERC20 tokens to the prize receiver account address.

The receiver might be a smart contract not prepared to claim the tokens, as the described behavior by the documentation is that the club owner would be the one performing the claim.

## Vulnerability Detail

The claim functions check `_to != msg.sender` to validate the receiver, instead of checking if they are the club owner:

```solidity
    function claimERC20Prize(
        address _to,
        IERC20Upgradeable _token,
        uint256 _amount,
        bytes32[] calldata _proof
    ) external whenNotPaused nonReentrant {
        if (_to != msg.sender) { // @audit
            revert InvalidAccount();
        }

        if (
            !MerkleProofUpgradeable.verify(
                _proof,
                erc20MerkleRoot,
                keccak256(abi.encodePacked(_token, _to, _amount))
            )
        ) {
            revert InvalidERC20MerkleProof();
        }

        uint256 value = _amount - totalERC20Claimed[_token][_to];

        if (value > 0) {
            totalERC20Claimed[_token][_to] += value;
            _token.transfer(_to, value);
        }

        emit ClaimERC20(_token, _to, value);
    }
```

## Impact

Tokens and Ether prizes might be unclaimable if the receiver is not prepared to do so, considering that the expected behavior is that the club owner should be the responsible to do so.

If a club owner changes, the assets are claimable by the original receiver, and not for the club (via its owner).

## Code Snippet

https://github.com/sherlock-audit/2023-04-footium/blob/main/footium-eth-shareable/contracts/FootiumPrizeDistributor.sol#L106-L134

https://github.com/sherlock-audit/2023-04-footium/blob/main/footium-eth-shareable/contracts/FootiumPrizeDistributor.sol#L144-L175

## Tool used

Manual Review

## Recommendation

Validate that the `msg.sender` is the club owner, as described in the documentation. This also allows the club owner to specify any address where they would like to receive the assets.

Here's an example for `claimERC20Prize()`. A similar implementation should be also done for `claimERC20Prize()`.

```diff
    function claimERC20Prize(
+       uint256 _clubId,
        address _to,
        IERC20Upgradeable _token,
        uint256 _amount,
        bytes32[] calldata _proof
    ) external whenNotPaused nonReentrant {
-       if (_to != msg.sender) {
-            revert InvalidAccount();
-        }
+       if (msg.sender != IERC721(footiumClubAddress).ownerOf(_clubId)) {
+           revert NotClubOwner(_clubId, msg.sender);
+       }

        if (
            !MerkleProofUpgradeable.verify(
                _proof,
                erc20MerkleRoot,
-               keccak256(abi.encodePacked(_token, _to, _amount))
+               keccak256(abi.encodePacked(_token, _clubId, _amount))
            )
        ) {
            revert InvalidERC20MerkleProof();
        }

-       uint256 value = _amount - totalERC20Claimed[_token][_to];
+       uint256 value = _amount - totalERC20Claimed[_token][_clubId];

        if (value > 0) {
-           totalERC20Claimed[_token][_to] += value;
+           totalERC20Claimed[_token][_clubId] += value;
            _token.transfer(_to, value);
        }

        emit ClaimERC20(_token, _to, value);
    }
```