GimelSec

medium

# When calling `FootiumGeneralPaymentContract.makePayment`, the caller should be able to ensure that `paymentReceiverAddress` is not changed.

## Summary

When calling `FootiumGeneralPaymentContract.makePayment`, the user is making a payment for the given feature. The `msg.value` would be sent to `paymentReceiverAddress`. However, if the owner of `FootiumGeneralPaymentContract` is changing the address. The transaction can happen before the transaction of `FootiumGeneralPaymentContract.makePayment`. Therefore, the user may send the value to the undesired address.

## Vulnerability Detail

The user calls `FootiumGeneralPaymentContract.makePayment` and sends the native tokens to `paymentReceiverAddress`. However, the address could change before the transaction is executed.
https://github.com/sherlock-audit/2023-04-footium/blob/main/footium-eth-shareable/contracts/FootiumGeneralPaymentContract.sol#L84
```solidity
    function makePayment(uint256 _clubId, string calldata _message)
        external
        payable
        whenNotPaused
        nonReentrant
    {
        if (msg.sender != footiumClub.ownerOf(_clubId)) {
            revert NotClubOwner(_clubId, msg.sender);
        }

        if (msg.value <= 0) {
            revert IncorrectETHAmount(msg.value);
        }

        (bool sent, ) = paymentReceiverAddress.call{value: msg.value}("");
        if (!sent) {
            revert FailedToSendETH(msg.value);
        }

        emit PaymentMade(_clubId, msg.value, _message);
    }
```
`setPaymentReceiverAddress` can be called by the owner anytime. Even the contract is not paused.
https://github.com/sherlock-audit/2023-04-footium/blob/main/footium-eth-shareable/contracts/FootiumGeneralPaymentContract.sol#L84
```solidity
    function setPaymentReceiverAddress(address _paymentReceiverAddress)
        external
        onlyOwner
    {
        paymentReceiverAddress = _paymentReceiverAddress;

        emit PaymentReceiverUpdated(paymentReceiverAddress);
    }
```

## Impact

Users may send the native tokens to the address that they don’t agree with. It’s a potential loss of funds.

## Code Snippet

https://github.com/sherlock-audit/2023-04-footium/blob/main/footium-eth-shareable/contracts/FootiumGeneralPaymentContract.sol#L84
https://github.com/sherlock-audit/2023-04-footium/blob/main/footium-eth-shareable/contracts/FootiumGeneralPaymentContract.sol#L52

## Tool used

Manual Review

## Recommendation

There are two methods to mitigate this issue.
First, Add `_paymentReceiverAddress` into the parameters of `makePayment`. Then, the address can be checked
```solidity
    function makePayment(uint256 _clubId, string calldata _message, address _paymentReceiverAddress)
        external
        payable
        whenNotPaused
        nonReentrant
    {
        if (msg.sender != footiumClub.ownerOf(_clubId)) {
            revert NotClubOwner(_clubId, msg.sender);
        }

        if (msg.value <= 0) {
            revert IncorrectETHAmount(msg.value);
        }

        if (_paymentReceiverAddress != paymentReceiverAddress) {
            revert AddressChanged(_paymentReceiverAddress, paymentReceiverAddress);
        }
        …
    }
```


Second, add the `whenPaused` modifier on `setPaymentReceiverAddress`. So `paymentReceiverAddress ` can only be changed when the contract is paused. And users cannot call `makePayment` when the contract is paused.
```solidity
    function setPaymentReceiverAddress(address _paymentReceiverAddress)
        external
        onlyOwner
        whenPaused
    {
        paymentReceiverAddress = _paymentReceiverAddress;

        emit PaymentReceiverUpdated(paymentReceiverAddress);
    }
```
